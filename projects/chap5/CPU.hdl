// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/05/CPU.hdl

/**
 * The Central Processing unit (CPU).
 * Consists of an ALU and a set of registers, designed to fetch and 
 * execute instructions written in the Hack machine language.
 * In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM=0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time unit. If reset=1 then the 
 * CPU jumps to address 0 (i.e. sets pc=0 in next time unit) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset=1) or continue executing
                         // the current program (reset=0).

    OUT outM[16],        // M value output
        writeM,          // Write into M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // first, we gotta decode to the global scope.
    // use chip decoder.hdl
    Decoder(Ins=instruction, i=i, a=a, c1=c1, c2=c2, c3=c3, c4=c4, c5=c5, c6=c6,
    d1=d1, d2=d2, d3=d3, j1=j1, j2=j2, j3=j3);
    
    And(a=d3, b=i, out=writeM);

    Mux16(a=instruction, b=outALU, sel=i, out=toA); // select A/C instruction
        Not(in=i, out=NOTi);
        Or(a=NOTi, b=d1, out=Aload);
    ARegister(in=toA, load=Aload, out=outA, out[0..14]=addressM); // the A register
    Mux16(a=outA, b=inM, sel=a, out=AorM); // selects A or M to ALU
    ALU(x=outD, y=AorM, zx=c1, nx=c2, zy=c3, ny=c4, f=c5, no=c6,
        out=outALU, out=outM, zr=zr, ng=ng);
    And(a=d2, b=i, out=writeD);
    DRegister(in=outALU, load=writeD, out=outD); // the D register
    
    PCenl(ng=ng, zr=zr, j1=j1, j2=j2, j3=j3, load=loadPCmb);
    And(a=loadPCmb, b=i, out=loadPC);
    PC(in=outA, load=loadPC, reset=reset, inc=true, out[0..14]=pc);
    /* make sure it's okay for inc to be always true */
}
